plugins {
  id "java"
  id "distribution"
  id "com.palantir.docker" version "0.19.2"
}

ext {
  adpCoreVersion = project.hasProperty('adpCoreVersion') ? project.getProperty('adpCoreVersion') : '3.8-SNAPSHOT'
}

distTar.enabled=false
distZip.enabled=false
dockerfileZip.enabled=false
compileJava.enabled=false
processResources.enabled=false
classes.enabled=false
jar.enabled=false

configurations {
  antJunit
}

repositories {
  mavenCentral()
  maven { url "https://nexus.adaptris.net/nexus/content/groups/public" }
  maven { url "https://nexus.adaptris.net/nexus/content/repositories/snapshots" }
  maven { url "https://nexus.adaptris.net/nexus/content/repositories/releases" }
}

dependencies {
  testCompile ("com.adaptris:interlok-service-tester:$adpCoreVersion") { changing= true}
  antJunit ("org.apache.ant:ant-junit:1.8.2")
}

task interlokServiceTest(overwrite: true) {
  doLast {
    javaexec {
      main = 'com.adaptris.tester.runners.TestExecutor'
      classpath = sourceSets.test.runtimeClasspath
      args "--serviceTest"
      args "$projectDir/src/test/interlok/service-test.xml"
      args "--serviceTestOutput"
      args "$buildDir/test-results/"
    }
  }
}

task junitReport {
  doLast {
    ant.taskdef(
      name: 'junitreport',
      classname: 'org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator',
      classpath: configurations.antJunit.asPath
    )

    ant.junitreport(todir: "$buildDir/test-results") {
      fileset(dir: "$buildDir/test-results", includes: 'TEST-*.xml')
      report(todir: "$buildDir/test-results/html", format: "frames"	)
    }
  }
}

check.dependsOn += interlokServiceTest
interlokServiceTest.dependsOn += processTestResources
interlokServiceTest.finalizedBy junitReport

distributions {
  main {
    contents {
      from {
        "src/main/interlok"
      }
      into('lib') {
        from(project.configurations.runtime)
      }
      into('/') {
        from file("$projectDir/src/main/resources/docker-entrypoint-wrapper.sh")
      }
      rename '(.*)-[0-9]+\\..*.jar', '$1.jar'
    }
  }
}

docker {
  name "adaptrislabs/interlok-hello-world"
  tags 'latest'
  pull true
  files tasks.installDist.outputs
}

docker.dependsOn installDist
installDist.dependsOn interlokServiceTest
